; Install program generation script for KevEdit in windows
; Requires Inno Setup

[Setup]
AppName=KevEdit
AppVerName=KevEdit @VERSION@
AppPublisher=Kev Vance and Ryan Phillips
AppPublisherURL=http://github.com/cknave/kevedit
AppSupportURL=https://github.com/cknave/kevedit/issues
AppUpdatesURL=https://github.com/cknave/kevedit/releases
DefaultDirName={pf}\KevEdit
DefaultGroupName=Games\ZZT
LicenseFile=Z:\work\COPYING.txt
InfoBeforeFile=Z:\work\README.md
OutputDir=Z:\dist
; Cosmetics
WindowVisible=no
WizardImageFile=Z:\platform\windows\install-big.bmp
WizardSmallImageFile=Z:\platform\windows\install-small.bmp

[Files]
Source: "Z:\work\AUTHORS.txt";  DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\work\ChangeLog.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\work\COPYING.txt";  DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\work\kevedit\src\kevedit\kevedit.exe";  DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\work\kevedit\docs\kevedit.zml";  DestDir: "{app}"; Flags: ignoreversion
Source: "Z:\work\README.md";   DestDir: "{app}"; Flags: ignoreversion isreadme
Source: "Z:\work\sdl\README-SDL.txt"; Destdir: "{app}"; Flags: ignoreversion
Source: "Z:\work\sdl\SDL2.dll";      Destdir: "{app}"; Flags: ignoreversion

[Icons]
Name: "{group}\KevEdit"; Filename: "{app}\kevedit.exe"; WorkingDir: "{userdocs}"
Name: "{group}\KevEdit Readme";  Filename: "{app}\README.md"
Name: "{group}\Uninstall KevEdit"; Filename: "{uninstallexe}"

[Run]
Filename: "{app}\kevedit.exe"; Description: "Launch kevedit"; WorkingDir: "{userdocs}"; Flags: nowait postinstall skipifsilent

[Registry]
; Associate KevEdit with ZZT worlds
Root: HKCR; Subkey: ".zzt"; ValueType: string; ValueData: "zztfile"
Root: HKCR; Subkey: ".sav"; ValueType: string; ValueData: "zztsave"; Flags: createvalueifdoesntexist

Root: HKCR; Subkey: "zztfile\Shell\KevEdit"; ValueType: string; ValueData: "KevEdit"; Flags: uninsdeletekey
Root: HKCR; Subkey: "zztfile\Shell\KevEdit\Command"; ValueType: string; ValueData: "{app}\kevedit.exe ""%1"""; Flags: uninsdeletekey

Root: HKCR; Subkey: "zztsave\Shell"; ValueType: string; ValueData: "KevEdit"; Flags: uninsdeletekey
Root: HKCR; Subkey: "zztsave\Shell\KevEdit"; ValueType: string; ValueData: "KevEdit"; Flags: uninsdeletekey
Root: HKCR; Subkey: "zztsave\Shell\KevEdit\Command"; ValueType: string; ValueData: "{app}\kevedit.exe ""%1"""; Flags: uninsdeletekey

